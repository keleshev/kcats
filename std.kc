#
#                     --- kcats standard library ---
# 
# Syntax:
# 
#     <command> 
#         Enter a textual <command> or several to execute on stack.
#         E.g. `fact` command will yield factorial of top-of-stack.
# 
#     <number>
#         Enter a <number> to push it on stack.
#         E.g. `1 500 10e-35 inf nan` will push the corresponding
#         floating-point values on stack.
# 
#     [<number>] 
#         Push a copy of <number>-th element on stack.
#         E.g. if stack consist of `2 4 8`, [1] will push the first
#         number (8) on top of stack: `2 4 8 8`.
# 
#     <name>: <commands> return 
#         Definition of <name> command, <commands> will be executed 
#         until 'return' keyword is encountered.
# 
#     if <condition> then <commands> return <other-commands>
#         If <condition> commands yields a non-zero value, 
#         then <commands> are executed until 'return' is encounterd;
#         if <condition> yields zero value, <other-commands> are
#         executed (commands after the closest 'return').
# 
#     . (i.e. dot) 
#         A short synonim for 'return' keyword.
#     
#     : (i.e. colon)
#         A short synonim for 'then' keyword.                
#     
#     <filename>.kc (TODO implement)
#         Load definitions from .kc file with <filename>-name.
#         E.g. 'math.kc' will load file math.kc from current directory.
#     
#     # (i.e. hash)
#         Unconditional jump til next newline character.
#         Could be used for comments.
# 
# Special commands:
#     q - quit kcats.
#     context - show executing context (definitions).
#     help - same as 'context' (TODO implement).
#     c - clear stack.
#     x - delete/pop top element from stack.
#     swp - swap two top elements on stack.
# 
# Builtins:
#     +  (sum)
#     ^  (power)
#     *  (product)
#     /  (division)
#     >  (greater-than)

# 
# Standard definitons:
#

# Constants:

true:  1 . 
false: 0 . 

e:  2.71822 . 
pi: 3.14159 . 

# Syntactic sugar for duplication-d and deletion-x

d: [1] .
dd: [2] [2] .
ddd: [3] [3] [3] .
xx: x x .
xxx: x x x .
s: swp .


# Logical operations:

not:  0 == .  
!:    not .         # '!' short for 'not'
bool: not not . 
and:  * bool . 
or:   ! s ! and ! .
nor: or not .


if:   .     # Guess why 'if' and 'else'
else: .     # have such strange definitions?
pass: .

# Comparison operators:

<:  s > .      # less-than
>=: < ! .
<=: > ! .
==: 
    if dd > : xx false . 
    if dd < : xx false . 
              xx true  .
!=: == ! .      # not equal


# Math:

-: -1 * + .    # substraction
max: 
    if dd > : x . s x .
min:
    if dd < : x . s x .
sign: 
    if d 0 > : x +1 .
    if d 0 < : x -1 . 
            else x  0 .
abs: 2 ^ 0.5 ^ .
abs2: if d 0 > : pass . -1 * .
abs3: d sign * .


# Assert equal (draft, requires strings):
# 
# assert: 
#   if dd == : x .     # if equal - do nothing
#   else "assert error " pr pr " != " pr pr .     

# Factorial - long version

factoria: 
    if d 0 == then        
        x 1                 
    return
    else                    
        d 1 - factoria *    
    return


# Fact. shrt. ver.

fact: d 0 == : x 1 .                  
      d 1 - fact * .                  

# Note that factorial versions above are identical
# except for keywords usage.

# Calulate factorial for values 1 to 10

f10: 0 _f10 .

_f10:
    if d 10 == : x .  
    1 + d fact s _f10 . 


# Fibonacci function

fib:
    if d 0 == : pass .
    if d 1 == : pass .
  else d 1 - fib s 2 - fib + .

longword: 10 20 30 .


